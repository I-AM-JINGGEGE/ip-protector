// build properties
def buildProperties = new Properties()
buildProperties.load(new FileInputStream(rootProject.file('build.properties')))

// keystore properties
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file('keystore.properties')))

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId rootProject.applicationId

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode buildProperties.versionCode.toInteger()

        buildConfigField "String", "PUBLISH_SENCONDS", "\"" + System.currentTimeSeconds().intValue() + "\""

        // for bundle
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

//        // for apk
//        ndk {
//            abiFilters 'armeabi-v7a'
//        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        doNotStrip "lib/*/libimc.so"
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    viewBinding {
        enabled = true
    }
    
    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions "channel"
    productFlavors {
        gp {
            buildConfigField "String", "CNL", "\"gp\""
            versionName buildProperties.versionName
        }
        xiaomi {
            buildConfigField "String", "CNL", "\"dtneaa1461b\""
            versionName "Xiaomi_" + buildProperties.versionName
        }
        transsion {
            buildConfigField "String", "CNL", "\"dtn9a658c8e\""
            versionName "Transsion_" + buildProperties.versionName
        }
        samsung {
            buildConfigField "String", "CNL", "\"dtn08376b2d\""
            versionName "Samsung_" + buildProperties.versionName
        }
    }
    ndkVersion '20.1.5948944'
}

dependencies {
    // androidx
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.appcompat:appcompat:1.4.0'

    // androidx work manager
    implementation "androidx.work:work-runtime:2.7.1"

    // androidx lifecycle
    implementation("androidx.lifecycle:lifecycle-process:${rootProject.lifecycleVersion}")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:${rootProject.lifecycleVersion}")

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"

    // utils
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.tencent:mmkv-static:1.2.14'

    implementation(platform("com.google.firebase:firebase-bom:31.0.3"))
    // firebase
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // ironmeta tahiti sdk
//    implementation project(path: ':ironmeta')

    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'com.android.installreferrer:installreferrer:2.2'

    // ImageLoader
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    //pag
    implementation 'com.tencent.tav:libpag:4.0.5.82'

    //google admob
    implementation("com.google.android.gms:play-services-ads:22.0.0")

    //roiquery
    implementation 'com.lovinjoy:datatowerai-core:3.0.1'

    implementation 'com.adjust.sdk:adjust-android:4.33.3'

    implementation("com.github.JessYanCoding:AndroidAutoSize:v1.2.1")

    //retrofit
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation("com.squareup.retrofit2:converter-gson:2.9.0")

    implementation 'com.geyifeng.immersionbar:immersionbar:3.2.2'
    implementation 'com.geyifeng.immersionbar:immersionbar-ktx:3.2.2'

    implementation 'com.jaeger.statusbarutil:library:1.5.1'
    implementation 'com.google.android.ump:user-messaging-platform:2.2.0'

    //vpn sdk for google play bundle release
    releaseImplementation('ironmeta:ironmeta-iphider-ss-rust-sdk-meta:nightly-20240711-1100-release') {
        exclude group: "org.connectbot.jsocks", module: "jsocks"
    }
    //vpn sdk for debug
    debugImplementation('ironmeta:ironmeta-iphider-ss-rust-sdk-meta:nightly-20240711-1030-debug') {
        exclude group: "org.connectbot.jsocks", module: "jsocks"
    }
    implementation("com.google.android.play:review:2.0.1")
    implementation("com.google.android.play:review-ktx:2.0.1")
}
